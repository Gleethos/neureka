package ut.calculus

import neureka.Neureka
import neureka.Tsr
import neureka.autograd.ADAgent
import neureka.calculus.Function
import neureka.calculus.environment.ExecutionCall
import neureka.calculus.environment.OperationTypeImplementation
import neureka.calculus.environment.implementations.Activation
import neureka.calculus.environment.implementations.Convolution
import neureka.calculus.environment.implementations.Operator
import neureka.calculus.environment.operations.OperationContext
import spock.lang.Specification

class Calculus_Implementation_AD_Unit_Tests extends Specification
{

    def 'Operator implementations behave as expected.'(
            OperationTypeImplementation imp
    ){

        given : 'The current Neureka instance is being reset.'
            Neureka.instance().reset()

        and : 'A mock Function.'
            def function = Mock(Function)
            def derivative = Mock(Tsr)
            function.derive(*_) >> derivative

        and : 'A mock ExecutionCall.'
            def call = Mock(ExecutionCall)

        when : 'A new ADAgent is being instantiated by calling the given implementation with these arguments...'
            ADAgent agent = imp.ADAgentCreator.getADAgentOf(
                    function,
                    null,
                    call,
                    true
            )

        then : 'The agent is configured to perform forward-AD and it contains the derivative generated by the function!'
            agent.isForward()
            agent.derivative() == derivative

        when : 'The agent generator is called once more with the forward flag set to false...'
            agent = imp.ADAgentCreator.getADAgentOf(
                    function,
                    null,
                    call,
                    false
            )

        then : 'The result is similar except the agent is not configured to perform forward-AD as was the case previously.'
            !agent.isForward()
            agent.derivative() == derivative

        where : 'The variable "imp" is from a List of OperationType implementations of type "Operator".'
            imp << OperationContext.instance()
                    .instances()
                    .stream()
                    .filter(
                            e ->
                                    e.isOperator() &&
                                            e.identifier().length()==1 &&
                                                e.supports( Operator.class )
                    ).map( e -> e.getImplementation( Operator.class ) )
    }


    def 'Activation implementations behave as expected.'(
            OperationTypeImplementation imp
    ){

        given : 'The current Neureka instance is being reset.'
            Neureka.instance().reset()

        and : 'A mock Function.'
            def function = Mock(Function)
            def derivative = Mock(Tsr)
            function.derive(*_) >> derivative

        and : 'A mock ExecutionCall.'
            def call = Mock(ExecutionCall)

        when : 'A new ADAgent is being instantiated by calling the given implementation with these arguments...'
            ADAgent agent = imp.ADAgentCreator.getADAgentOf(
                function,
                null,
                call,
                true
            )

        then : 'The agent is configured to perform forward-AD and it contains the derivative generated by the function!'
            agent.isForward()
            agent.derivative() == derivative

        when : 'The agent generator is called once more with the forward flag set to false...'
            agent = imp.ADAgentCreator.getADAgentOf(
                function,
                null,
                call,
                false
            )

        then : 'The result is similar except the agent is not configured to perform forward-AD as was the case previously.'
            !agent.isForward()
            agent.derivative() == derivative

        where : 'The variable "imp" is from a List of OperationType implementations of type "Activation".'
            imp << OperationContext.instance()
                .instances()
                .stream()
                .filter(
                        e ->
                                        e.supports( Activation.class )
                ).map( e -> e.getImplementation( Activation.class ) )
    }


    def 'Convolution implementations behave as expected.'(
            OperationTypeImplementation imp
    ){

        given : 'The current Neureka instance is being reset.'
            Neureka.instance().reset()

        and : 'A mock Function.'
            def function = Mock(Function)
            def derivative = Mock(Tsr)
            function.derive(*_) >> derivative

        and : 'A mock ExecutionCall.'
            def call = Mock(ExecutionCall)

        when : 'A new ADAgent is being instantiated by calling the given implementation with these arguments...'
            ADAgent agent = imp.ADAgentCreator.getADAgentOf(
                    function,
                    null,
                    call,
                    true
            )

        then : 'An exception is being thrown because implementations of type "Convolution" can only perform reverse mode AD!'
            def exception = thrown(IllegalArgumentException)
            exception.message == "Convolution of does not support forward-AD!"

        when : 'The agent generator is called once more with the forward flag set to false...'
            agent = imp.ADAgentCreator.getADAgentOf(
                function,
                null,
                call,
                false
            )

        then : 'No exception is being thrown and the agent is configured to perform backward-AD.'
            !agent.isForward()
            agent.derivative() == derivative

        where : 'The variable "imp" is from a List of OperationType implementations of type "Convolution".'
            imp << OperationContext.instance()
                .instances()
                .stream()
                .filter(
                        e ->
                                e.isOperator() &&
                                        e.identifier().length()==1 &&
                                        e.supports( Convolution.class )
                ).map( e -> e.getImplementation( Convolution.class ) )
    }




}
