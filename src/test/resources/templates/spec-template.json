<%

def parseWhereCodeLines = { List<String> lines ->
    String code = lines.join("\n")
    boolean isSimpleTable = (~/^\s*([a-zA-Z0-9_]+)\s*<<\s*/).matcher(code).find()
    Map<String, List<String>> table = [:]
    int roundDepth  = 0
    int squareDepth = 0
    int curlyDepth  = 0
    boolean isInsideRegularString = false
    boolean isInsideOtherString = false
    boolean previousCharWasEscape = false
    boolean previousWasSlash = false
    boolean previousWasPipe = false
    boolean isComment = false
    var currentExpression = ""
    var singleKey = "" // This is only used if the table is simple.
    int currentColumn = 0 // This is only used if the table is not simple.
    var columnNames = [] // This is only used if the table is not simple.
    boolean columnNamesRead = false // This is only used if the table is not simple.
    for ( int i = 0; i < code.length(); i++ ) {
        String c = String.valueOf(code.charAt(i))
        isInsideRegularString = c == "\"" && !previousCharWasEscape ? !isInsideRegularString : isInsideRegularString
        isInsideOtherString = c == "'" && !previousCharWasEscape && !isInsideRegularString ? !isInsideOtherString : isInsideOtherString
        var wasComment = isComment
        isComment = c != "\n" && (c == "/" && previousWasSlash || isComment )
        if ( isComment && !wasComment ) {
            if ( !currentExpression.isEmpty() ) // We remove the last slash character if it is not part of a comment.
                currentExpression = currentExpression.substring(0, currentExpression.length()-1)
        }
        var isInsideString = isInsideRegularString || isInsideOtherString
        boolean escaped = isInsideString || isComment
        if ( !escaped ) {
            roundDepth  += c == "(" ? 1 : c == ")" ? -1 : 0
            curlyDepth  += c == "{" ? 1 : c == "}" ? -1 : 0
            squareDepth += c == "[" ? 1 : c == "]" ? -1 : 0
        }
        boolean outsideOfAll = roundDepth == 0 && squareDepth == 0 && curlyDepth == 0
        if ( isSimpleTable ) {
            //~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
            if ( outsideOfAll ) {
                if ( c == "<" && !currentExpression.isEmpty() ) {
                    singleKey = currentExpression.trim()
                    table[singleKey] = []
                    currentExpression = ""
                }
                else if ( table.isEmpty() ) currentExpression += c
            }
            else if ( squareDepth == 1 && c == "," && !escaped ) {
                table[singleKey]?.add(currentExpression.trim())
                currentExpression = ""
            }
            else if ( squareDepth > 0 && c != "[" && !isComment )
                currentExpression += c
            //~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        } else {
            //~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
            if ( !columnNamesRead && outsideOfAll ) {
                if ( (c == "|" && !escaped || c == "\n" && !escaped) && !currentExpression.isEmpty() ) {
                    var foundColumnName = currentExpression.trim()
                    columnNames.add(foundColumnName)
                    table[foundColumnName] = []
                    currentExpression = ""
                    if ( c == "\n" )
                        columnNamesRead = true
                }
                else if ( c != "|" && !escaped  )
                    currentExpression += c
            }
            else if ( columnNamesRead && outsideOfAll ) {
                if ( c == "|" && !escaped ) {
                    if ( !previousWasPipe && !currentExpression.isEmpty() ) {
                        table[columnNames[currentColumn]]?.add(currentExpression.trim())
                        currentExpression = ""
                        currentColumn = (currentColumn + 1) % columnNames.size()
                    }
                }
                else if ( c == "\n" && !escaped ) {
                    table[columnNames[currentColumn]]?.add(currentExpression.trim())
                    currentExpression = ""
                    currentColumn = 0
                }
                else if ( c != "|" && !escaped )
                    currentExpression += c
            }
            else
                currentExpression += c
            //~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        }
        previousCharWasEscape = c == "\\"
        previousWasSlash = c == "/"
        previousWasPipe = c == "|"
    }
    if ( isSimpleTable && !currentExpression.isEmpty() )
        table[singleKey]?.add(currentExpression.trim())
    return table
}

def noSingleBreaks = { String s ->
    return s.replaceAll("\\n+\\s+\\n+", "\n\n").replaceAll("(\\n\\n)\\n*", "\n\n")
}
def simpleEsc = { text ->
    text = text.toString();
    if ( text == null )  return ""
    final StringWriter writer = new StringWriter()
    ( 0 ..< text.length() ).each { i ->
        final char c = text.charAt(i)
        switch (c) {
            case '"':  writer.write("\\\""); break;
            case '\\': writer.write("\\\\"); break;
            case '\n': writer.write("\\n"); break;
            case '\r': writer.write("\\r"); break;
            case '\t': writer.write("\\t"); break;
            case '\b': writer.write("\\b"); break;
            default:
                if ( c > 0x1f || c == '`' ) writer.write(c);
                else {
                    writer.write("\\u");
                    final String hex = "000" + Integer.toHexString(c as int);
                    writer.write(hex.substring(hex.length() - 4));
                }
        }
    }
    return writer.toString();
}
def esc = { simpleEsc(noSingleBreaks(it)) }
def strCode = { it == null || it.isEmpty() ? "" : (it.size() == 1 ? ('"'+simpleEsc(it[0])+'"') : ('"'+it.collect({simpleEsc(it)}).join('","'))+'"')}


println("Generating json for '"+utils.getSpecClassName( data )+"' now!")

def stats = utils.stats( data )
def subjects = utils.specAnnotation( data, spock.lang.Subject )?.value()
if ( subjects == null ) subjects = []
def specTitle = utils.specAnnotation( data, spock.lang.Title )?.value()
%>{
  "className":"${utils.getSpecClassName( data )}",
  "title":"<% if ( specTitle ) { out << esc(specTitle) } %>",
  "narrative":"<% if ( data.info.narrative ) { out << esc(data.info.narrative.stripIndent()) } %>",
  "subjects":[<% out << subjects.collect({'"'+it.getName()+'"'}).join(',') %>],
  "statistics":{
    "runs":"${stats.totalRuns}",
    "successRate":"${fmt.toPercentage(stats.successRate)}",
    "failures":"${stats.failures}",
    "errors":"${stats.errors}",
    "skipped":"${stats.skipped}",
    "duration":"${fmt.toTimeDuration(stats.time)}"
  },
<%
  def writeTagOrAttachment = {feature ->
    def tagsByKey = feature.tags.groupBy({ t -> t.key })
    out << '"tags":{'
    tagsByKey.each {key, values ->
      out << '"' << key.capitalize() << '"['
      out << values.collect({tag->esc(tag.url)}).collect({'"'+it+'"'}).join(",")
      out << '],'
    }
    out << '},'
    out << '"see":['
    if ( feature.attachments.size > 0 ) out << feature.attachments.collect({v->esc(v.url)}).collect({'"'+it+'"'}).join(",")
    out << '],'
  }
  def writeHeaders = { headers ->
    out << '  "headers":['
    if ( headers ) out << headers.collect({v->esc(v)}).collect({'"'+it+'"'}).join(",")
    out << '],'
  }
  def writeExtraInfo = { extraInfo ->
    out << '"extraInfo":['
    if ( extraInfo ) out << extraInfo.collect({v->esc(v)}).collect({'"'+it+'"'}).join(",")
    out << ']'
  }
  writeHeaders( utils.specHeaders( data ) )
  writeTagOrAttachment data.info
%>
  "features":[ <%
    def i = 0
    def size = 0
    features.eachFeature { name, result, blocks, iterations, params -> size++ }
    features.eachFeature { name, result, blocks, iterations, params ->
      var dataTable = [:]
      (0 ..< params.size()).each({ columIndex ->
        var column = []
        iterations.each { iteration ->
          column << iteration.dataValues[columIndex]
        }
        dataTable[params[columIndex]] = column
      })
      def feature = delegate
      def iterationTimes = iterations.collect { it.time ?: 0L }
      def totalTime = fmt.toTimeDuration( iterationTimes.sum() )
  %>
    {
      "id":"${esc(name)}",
      "result":"$result",
      "duration":"$totalTime",
      "iterations":{
      <%
      writeTagOrAttachment( feature )
      if (result != "IGNORED") {
        if ( utils.isUnrolled( feature ) )
          iterations.each { iter ->
            writeExtraInfo( utils.nextSpecExtraInfo( data, feature, iter.info ) )
          }
        else
            writeExtraInfo( utils.nextSpecExtraInfo( data, feature ) )
      }
      else writeExtraInfo( null )
      %>
      },
      "blocks":[<%
        out << blocks.collect({ b ->
            var kind = esc(b.kind.replace(':', '').toLowerCase())
            var code = '['+strCode(b.sourceCode)+']'
            if ( kind == 'where' ) {
                var table = dataTable
                if ( b.sourceCode.isEmpty() ) {
                  var newTable = parseWhereCodeLines(b.sourceCode)
                  if ( newTable.size() == dataTable.size() )
                    table = newTable // If they are not the same size then parsing went wrong, so we keep the original table
                }
                // We turn the data table into a json object:
                code = '{'+table.collect({ k, v -> '"'+k+'":['+strCode(v)+']' }).join(",")+'}'
            }
            return '\n        {' +
                  '"kind":"'  +  kind  + '",' +
                  '"text":"'  + esc(b.text)                                  + '",' +
                  '"code":' + code + '' + '}'
        }).join(",\n")
        %>
      ],<%
      def executedIterations = iterations.findAll {it.dataValues || it.errors}
      def problems = executedIterations.findAll {it.errors}
      def probValue;
      if ( problems == [] ) probValue = '{"dataValues":[], "errors":[]}'
      else probValue = '{"dataValues":['+strCode(problems.dataValues)+'], "errors":['+strCode(problems.errors)+']}';
%>
      "problems":$probValue
    }<%
    i++
    if( i < size ){ out << "," }
    %>
  <%
  } // FEATURE LOOP END
  %>
  ],
  "generator":"<%out << com.athaydes.spockframework.report.SpockReportExtension.PROJECT_URL%>"
}