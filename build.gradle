

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:6.2.2'
    }
}

//-----------------------------------------------------------------

plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'groovy'
    //Public deployment:
    id 'maven-publish'
    id 'signing'
}

String PROJECT = "neureka"
String VERSION = "0.0.0"
String GROUPID = "org.gleethos."+PROJECT

def change = { c -> new Object() {
            def between = { String a, String b -> new Object() {
                    def inFile = { String p ->
                        File file = new File("$rootDir" + p)
                        String[] parts = file.text.split(a)//[1]
                        file.write(parts[0] + a.replace("\\", "") + c(parts[0]) + b.replace("\\", "") + parts[1].split(b)[1])
                    }
                }
            }
        }
    }

change({VERSION}).between("production/neureka-", "\\.jar\\)").inFile("/README.md")
change({VERSION}).between("return \"", "\"").inFile("/src/main/resources/library_settings.groovy")


group GROUPID
version VERSION

//-----------------------------------------------------------------

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    jcenter { url "http://jcenter.bintray.com/" }
}

//-----------------------------------------------------------------

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'org.jetbrains:annotations:16.0.2'
    compile group: 'org.jocl', name: 'jocl', version: '2.0.2'
    compile group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.0-rc-3'
}

//-----------------------------------------------------------------

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("docs/coverage")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}
check.dependsOn jacocoTestReport

//-----------------------------------------------------------------
//LOCAL DEPLOYMENT:
jar {
    //destinationDirectory = file("$rootDir/build/libs")
    copy {
        from "$rootDir/build/libs/neureka-"+VERSION+".jar"
        into "$rootDir/production/lib"
    }
}


task ('proguard', type: proguard.gradle.ProGuardTask) {

    //Note: maybe consider configuration 'configuration.pro' as file
    verbose

    //allowaccessmodification

    // Specify the input jars, output jars, and library jars.
    // library jars files(configurations.compile.collect())

    injars  'build/libs/'+PROJECT+'-'+VERSION+'.jar'
    outjars 'production/min-lib/'+PROJECT+'-min-'+VERSION+'.jar'

    // Automatically handle the Java version of this build.
    if (System.getProperty('java.version').startsWith('1.')) {// Before Java 9, the runtime classes were packaged in a single jar file.
        libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    } else {// As of Java 9, the runtime classes are packaged in modular jmod files.
        libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
    }

    // Save the obfuscation mapping to a file, so we can de-obfuscate any stack
    // traces later on.
    // Note: Keep a fixed source file attribute and all line number tables to get line numbers in the stack traces.
    // Can be commented out if not interested in stack traces.

    printmapping 'production/min/'+PROJECT+'-min-'+VERSION+'.map'
    keepparameternames
    renamesourcefileattribute 'SourceFile'
    keepattributes 'Exceptions,InnerClasses,Signature,Deprecated,SourceFile,LineNumberTable,EnclosingMethod'

    keepattributes '*Annotation*' // Preserve all annotations.

    // Preserve all public classes, and their public and protected fields and
    // methods.

    keep 'public class * { \
        public protected *; \
    }'

    // Preserve all .class method names.

    keepclassmembernames 'class * { \
        java.lang.Class class$(java.lang.String); \
        java.lang.Class class$(java.lang.String, boolean); \
    }'

    // Preserve all native method names and the names of their classes.

    keepclasseswithmembernames includedescriptorclasses: true, 'class * { \
        native <methods>; \
    }'

    // Preserve the special static methods that are required in all enumeration classes.

    keepclassmembers allowoptimization: true, 'enum * { \
        public static **[] values(); \
        public static ** valueOf(java.lang.String); \
    }'

    // Explicitly preserve all serialization members. The Serializable interface
    // is only a marker interface, so it wouldn't save them.
    // We could comment this out if neureka library doesn't use serialization.
    // If the code contains serializable classes that have to be backward
    // compatible -> refer to the manual.

    keepclassmembers 'class * implements java.io.Serializable { \
        static final long serialVersionUID; \
        static final java.io.ObjectStreamField[] serialPersistentFields; \
        private void writeObject(java.io.ObjectOutputStream); \
        private void readObject(java.io.ObjectInputStream); \
        java.lang.Object writeReplace(); \
        java.lang.Object readResolve(); \
    }'

    // Note: library may contain more items that need to be preserved;
    // typically classes that are dynamically created using Class.forName:

    // keep 'public class com.example.MyClass'
    // keep 'public interface com.example.MyInterface'
    // keep 'public class * implements com.example.MyInterface'

    keep 'class org.jocl.** { *; }'
    keep 'interface org.jocl.** { *; }'
    keep 'class org.jetbrains.** { *; }'
    keep 'interface org.jetbrains.** { *; }'
    keep 'class groovy.lang.** { *; }'
    keep 'interface groovy.lang.** { *; }'

    //keep 'class java.lang.** { *; }'

    //dontwarn 'java.**'

    dontwarn 'org.jocl.**'
    dontwarn 'org.codehaus.**'
    dontwarn 'groovy.lang.**'
    dontwarn 'org.jetbrains.**'
}

task deploy() {
    dependsOn 'assemble'
    //dependsOn 'check'
    dependsOn 'jar'
    dependsOn 'proguard'
}

//-----------------------------------------------------------------
//MAVEN DEPLOYMENT:

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId GROUPID
            artifactId PROJECT
            version VERSION
            from components.java
            pom {
                name = PROJECT
                description = "A platform independent tensor library written in Java."
                url = "https://gleethos.github.io/neureka/index.html"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://raw.githubusercontent.com/Gleethos/neureka/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "Gleethos"
                        name = "Daniel Nepp"
                        email = "Gleethos@gmx.at"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "$projectDir/production/lib"
            def snapshotsRepoUrl = "$projectDir/production/lib"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            if (version.contains("min")) url = "$projectDir/production/min-lib"
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    destinationDir = new File("$projectDir/docs/jdocs")
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

//-----------------------------------------------------------------
test {
    //filter { includeTestsMatching "*" }
    minHeapSize = "1g"
    maxHeapSize = "5g"
    maxParallelForks = Math.floor(1+(Runtime.runtime.availableProcessors().intdiv(2) ?: 1)/2)
}
//-----------------------------------------------------------------

task createJDocs(type: Javadoc){
    destinationDir = new File("$projectDir/docs/jdocs")
    source = "$projectDir/src/main"
    //sourceSets.main.allJava
}